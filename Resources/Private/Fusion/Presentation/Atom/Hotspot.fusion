prototype(FormatD.HotspotEditor:Atom.Hotspot) < prototype(Neos.Fusion:Component) {

	@styleguide {
		title = 'Hotspot'
		description = 'This is a hotspot item'
		props {
            hoverTitle = 'Hover title'
			content = '<svg width="50" height="50" viewBox="0 0 41 42" fill="none" xmlns="http://www.w3.org/2000/svg">
					<ellipse cx="20.5833" cy="21.2286" rx="10.8088" ry="10.8" fill="white"/>
					<path d="M39 21.2286C39 31.3891 30.7561 39.6286 20.5833 39.6286C10.4106 39.6286 2.16667 31.3891 2.16667 21.2286C2.16667 11.0681 10.4106 2.82855 20.5833 2.82855C30.7561 2.82855 39 11.0681 39 21.2286Z" stroke="white" stroke-width="3" />
				</svg>'
			xPosition = '50'
			yPosition = '50'
			uri = 'http://www.google.de'
			identifier = 'a-identifier-23453462467'
		}
	}

	@propTypes {
		@strict = false
        hoverTitle = ${PropTypes.string}
		xPosition = ${PropTypes.string}
		yPosition = ${PropTypes.string}
		identifier = ${PropTypes.string}
		uri = ${PropTypes.string}
		content = ${PropTypes.string}
	}

    hoverTitle = ''
	xPosition = '50'
	yPosition = '50'
	uri = ''
	identifier = null
	content =  '<svg width="50" height="50" viewBox="0 0 41 42" fill="none" xmlns="http://www.w3.org/2000/svg">
					<ellipse cx="20.5833" cy="21.2286" rx="10.8088" ry="10.8" fill="white"/>
					<path d="M39 21.2286C39 31.3891 30.7561 39.6286 20.5833 39.6286C10.4106 39.6286 2.16667 31.3891 2.16667 21.2286C2.16667 11.0681 10.4106 2.82855 20.5833 2.82855C30.7561 2.82855 39 11.0681 39 21.2286Z" stroke="white" stroke-width="3" />
				</svg>'

	styleCoordinates = ${'top: ' + this.yPosition + '%; left: ' + this.xPosition + '%;'}
	inBackend = ${documentNode.context.inBackend}

	renderer = afx`
		<Neos.Fusion:Tag
				tagName={props.inBackend || !props.uri ? 'button' : 'a'}
				attributes.class.hotspot="hotspot"
				attributes.style={props.styleCoordinates}
				attributes.href={props.uri ? props.uri : null}
				attributes.type={!props.uri ? 'button' : null}
				attributes.aria-controls={!props.uri ? props.identifier : null}
				attributes.aria-label={props.hoverTitle ? props.hoverTitle : 'Hotspot'}
				attributes.data-hotspot-posx={props.xPosition}
				attributes.data-hotspot-posy={props.yPosition}
				attributes.data-hotspot-id={props.identifier}
				@key="hotspot"
		>
			<div class.content="hotspot--content"
				 @key="hotspotContent"
			>
				{props.content}
			</div>
			<div class.title="hotspot--hover-title"
				 @if.isProvided={props.hoverTitle}
				 @key="hotspot--hover-title"
			>
				{props.hoverTitle}
			</div>
		</Neos.Fusion:Tag>
	`


}
